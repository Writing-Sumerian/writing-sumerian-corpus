start       : _unit ( _sep _unit )*

_unit       : line
            | paren
            | char
            | pseudo

line        : "[" ( paren | char | pseudo ) ( _sep ( paren | char| pseudo ) )* "]"
paren       : "(" _unit ( _sep _unit )* ")" [wildcard]
char        : spec+ [wildcard]
            | indicator [wildcard]
pseudo      : "p"

indicator   : [ALIGNMENT] INDIC_TYPE spec+

spec        : "v" NUMBER                    -> value
            | "s" NUMBER                    -> sign_variant
            | "g" NUMBER                    -> grapheme
            | "c" NUMBER                    -> glyph
            | "x"                           -> valuex
            | "X"                           -> signx
            | "n"                           -> numberx

wildcard    : "@" NUMBER

_sep        : ( " " | con | bar | colon | ellipsis | wordbreak | linebreak | inheritcon )+

con         : "-"
ellipsis    : "â€¦"
colon       : ":"
wordbreak   : ";"
linebreak   : "/"
bar         : "|"
inheritcon  : "~"

TYPE        : /[vsgcxXnp]/
NUMBER      : /[1-9][0-9]*|0/
INDIC_TYPE  : /[DP]/
ALIGNMENT   : /[<>]/